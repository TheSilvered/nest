|#| '../test_lib.nest' = test
|#| 'stdio.nest' = io

#read_whole_file f [
    f @io.get_fpi = start
    f io.FROM_START 0 @io.move_fpi
    f @io.read = content
    f io.FROM_START start @io.move_fpi
    => content
]

{72b, 105b} = bytes -- Hi

'test_files/file.txt' 'wb+' @io.open = f
f bytes @io.write_bytes
f @io.close

'test_files/file.txt' 'rb' @io.open = f
f -1 @io.read_bytes bytes @test.assert_eq
f @io.close

'test_files/file.txt' 'w' @io.open = f
f 'Hello' @io.write
f @io.close

f @io.get_flags '-w-s-' @test.assert_eq

'test_files/file.txt' 'r+' @io.open = f

f -1 @io.read 'Hello' @test.assert_eq
f @io.file_size 5 @test.assert_eq
f io.FROM_CUR -2 @io.move_fpi
f @io.get_fpi 3 @test.assert_eq
f -1 @io.read 'lo' @test.assert_eq

f @io._set_stdout
', world!' @io.println
io.STDOUT @io._set_stdout

f @read_whole_file 'Hello, world!\n' @test.assert_eq

f @io.close

false @io.virtual_file = f
f 'Hello' @io.write
f @io.flush
f @read_whole_file 'Hello' @test.assert_eq
f @io.get_flags 'rw-s-' @test.assert_eq
f @io.close

true @io.virtual_file = f
f @io.get_flags 'rwbs-' @test.assert_eq
f @io.close

'test_files/utf16le_file.txt' 'w' 'utf16le' @io.open = f
f 'àèìòù§°ç' @io.write
f @io.close

'test_files/utf16le_file.txt' 'r' 'utf16le' @io.open = f
f 3 @io.read 'àèì' @test.assert_eq
f @io.read 'òù§°ç' @test.assert_eq
f @io.close

