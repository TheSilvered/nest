|#| 'stdfs.nest' = fs
|#| 'stdio.nest' = io
|#| 'stdsutil.nest' = su
|#| 'stdsys.nest' = sys
|#| 'stderr.nest' = err
|#| 'stdsequtil.nest' = sequ
|#| 'test_lib.nest' = test

-- Creates or clears the log file
'test_output.txt' 'w' @io.open = test.log_file

sys.SUPPORTS_COLOR ? [
    '\e[31m' = C_RED
    '\e[32m' = C_GREEN
    '\e[94m' = C_BLUE
    '\e[0m' = C_RESET
] : [
    '' = C_RED
    '' = C_GREEN
    '' = C_BLUE
    '' = C_RESET
]

0 = failed_tests

#run_test name path [
    !(name 'test_e_' @su.starts_with) ?
        |#| path
    : [
        ?? |#| path
        ?! e =>
        'Assertion Error' !! ('the file ' name ' should raise an error' ><)
    ]
]

#error_location traceback [
    !traceback ? => '; <unknown position>'
    ... traceback @sequ.reverse_i := tb_pos [
        tb_pos.file @fs.filename 'test_lib.nest' != ? [
            tb_pos = pos
            ;
        ]
    ]
    => '; line ' (pos.start.0 1 +) ' column ' (pos.start.1 1 +) ' file ' (pos.file @fs.filename @su.repr) ><
]

#run_tests dir [
    C_BLUE '** Testing directory ' (dir @su.repr) ' **' C_RESET >< @io.println

    ... Iter :: (dir @fs.list_dirs) := test_path [
        test_path @fs.extension '.nest' != ? ..
        test_path @fs.filename = name
        !(name 'test_' @su.starts_with) ? ..

        >>> ('Testing ' (name 5 -5 @sequ.slice) '...' ><)
        run_test {name, test_path} @err.try = res

        res.error null == ? [
            C_GREEN ' PASSED' C_RESET >< true @io.println
            ..
        ]

        1 += _globals_.failed_tests

        C_RED ' FAILED' C_RESET ': ' res.error.name ' - ' res.error.message >< = s
        res.traceback @error_location ><= s
        s true @io.println
    ]
]

sys.PLATFORM 'windows' == ? [
    '.\\object_tests' @run_tests
    '.\\std_lib_tests' @run_tests
    '.\\error_tests' @run_tests
] : [
    './object_tests' @run_tests
    './std_lib_tests' @run_tests
    './error_tests' @run_tests
]

test.log_file @io.flush
test.log_file @io.close

failed_tests 0 == ?
    C_GREEN "\nAll tests passed." C_RESET >< @io.println
: failed_tests 1 == ?
    C_RED "\n1 test failed." C_RESET >< @io.println
:
    C_RED "\n" failed_tests " tests failed." C_RESET >< @io.println

failed_tests @sys.exit
