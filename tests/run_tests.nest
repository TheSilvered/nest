|#| 'stdfs.nest' = fs
|#| 'stdio.nest' = io
|#| 'stdsutil.nest' = su
|#| 'stdsys.nest' = sys
|#| 'test_lib.nest' = test
|#| 'stdtime.nest' = dt

-- Creates or clears the log file
'test_output.txt' 'w' @io.open = test.log_file

#run_tests dir [
    ... Iter :: (dir @fs.list_dir_recursive) := test_path [
        test_path @fs.extension '.nest' != ? ..

        test_path @fs.filename = name

        !(name 'e_' @su.starts_with) ?
            |#| test_path
        : [
            ?? [
                -- runs the file
                |#| test_path
            ]
            ?! e [
                name true @io.println
                ..
            ]
            'Assertion Error' !! ('the file ' name ' should raise an error' ><)
        ]
        name true @io.println
    ]
]

sys.PLATFORM 'windows' == ? [
    '.\\object_tests' @run_tests
    '.\\std_lib_tests' @run_tests
    '.\\error_tests' @run_tests
] : [
    './object_tests' @run_tests
    './std_lib_tests' @run_tests
    './error_tests' @run_tests
]

test.log_file @io.flush
test.log_file @io.close
