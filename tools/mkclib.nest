|#| 'stdio.nest' = io
|#| 'stdfs.nest' = fs
|#| 'stdsutil.nest' = su

#replace_file_str path s1 s2 [
    path 'r' @io.open = file
    file -1 @io.read s1 s2 @su.replace_substr = file_content
    file @io.close

    path 'w' @io.open = file
    file file_content @io.write
    file @io.close
]

{
    --    Headers          Source    --
    'error.h',          'error.c',
    'error_internal.h',
    'function.h',       'function.c',
    'hash.h',           'hash.c',
    'iter.h',           'iter.c',
    'interpreter.h',    'interpreter.c',
    'lexer.h',          'lexer.c',
    'lib_import.h',     'lib_import.c',
    'llist.h',          'llist.c',
    'map.h',            'map.c',
    'nest_include.h',
    'nodes.h',          'nodes.c',
    'nst_types.h',
    'obj.h',            'obj.c',
    'obj_ops.h',        'obj_ops.c',
    'parser.h',         'parser.c',
    'sequence.h',       'sequence.c',
    'simple_types.h',   'simple_types.c',
    'str.h',            'str.c',
    'tokens.h',         'tokens.c',
    'var_table.h',      'var_table.c'
} = nest_source_files

#main [
    ($_args_ 3 !=) ($_args_ 4 !=) && ? [
        >>> 'USAGE: nest .\\make_clib_files.py (new <library_name>|update) <location>\n'
        => 1
    ]

    _args_.1 = operation
    ($_args_ 4 == ? _args_.2 : null) = project_name
    _args_.($_args_ 4 == ? 3 : 2) = location

    (operation 'new' !=) (operation 'update' !=) && ? [
        >>> ('The given operation ("'operation'") is not valid\n' ><)
        => 1
    ]

    !(location @fs.isdir) ? [
        >>> ('The given path ("'location'") doesn\'t exist\n' ><)
        => 2
    ]

    (location'\\nest_source' >< = srcdir) @fs.mkdir

    ... Iter :: nest_source_files ~= file_name [
        ('..\\nest\\'file_name >< 'r' @io.open = src_f) -1 @io.read = content
        src_f @io.close
        (srcdir'\\'file_name >< 'w' @io.open = dst_f) content @io.write
        dst_f @io.close
    ]

    operation 'update' == ? => 0

    project_name ' ' '_' @su.replace_substr = project_name_c

    !(project_name_c (su.LETTERS su.DIGITS '_' ><) @su.is_charset) ? [
        >>> ('The given name ("'project_name'") is not valid\n' ><)
        => 3
    ]

    project_name_c @su.to_upper = upper_project_name

    location'\\'project_name'.h' >< 'w' @io.open = header_file
    header_file ("#ifndef "upper_project_name"_H
#define "upper_project_name"_H

#include \"nest_source/nest_include.h\"

#ifdef __cplusplus
extern \"C\" {
#endif // !__cplusplus

__declspec(dllexport) bool lib_init();
__declspec(dllexport) FuncDeclr *get_func_ptrs();
__declspec(dllexport) INIT_LIB_OBJ_FUNC;

// Here you can put your function signatures
// They must always be `Nst_Obj *func_name(size_t arg_num, Nst_Obj **args, OpErr *err);`
// replace func_name with your function's name

#ifdef __cplusplus
}
#endif // !__cplusplus

#endif // !"upper_project_name"_H" ><) @io.write
    header_file @io.close

    location'\\'project_name'.cpp' >< 'w' @io.open = source_file
    source_file ("#include \""project_name".h\"

#define FUNC_COUNT // Set this to the number of functions in your module

static FuncDeclr *func_list_;
static bool lib_init_ = false;

bool lib_init()
{
    if ( (func_list_ = new_func_list(FUNC_COUNT)) == nullptr )
        return false;

    size_t idx = 0;

    // Set each function to an index in func_list_
    // func_list_[idx++] = {
    //     func_ptr, -> the function pointer
    //     1, -> the number of arguments the function takes
    //     new_string_raw(\"func_name\", false) -> the string containing the name
    // }                                           of the funtion inside Nest

    lib_init_ = true;
    return true;
}

FuncDeclr *get_func_ptrs()
{
    return lib_init_ ? func_list_ : nullptr;
}

// Here you can put the implementations of your functions" ><) @io.write
    source_file @io.close

    -- remove precompiled header files
    location'\\pch.cpp' >< @fs.isfile ? location'\\pch.cpp' >< @fs.rmfile
    location'\\pch.h'   >< @fs.isfile ? location'\\pch.h'   >< @fs.rmfile

    -- change '#include "pch.h" to #include "framework.h"' since pch.h has
    -- just been removed
    (location'\\dllmain.cpp' >< @fs.isfile) \
    (location'\\framework.h' >< @fs.isfile) && ? [
        location'\\dllmain.cpp' >< 'pch' 'framework' @replace_file_str
    ]

    -- Turn off precompiled headers
    location'\\'project_name_c'.vcxproj' >< \
    '<PrecompiledHeader>Use' \
    '<PrecompiledHeader>NotUsing' @replace_file_str

    -- Set language to C++ 20
    location'\\'project_name_c'.vcxproj' >< \
    '</PrecompiledHeaderFile>\n    </ClCompile>' \
    "</PrecompiledHeaderFile>
      <LanguageStandard>stdcpp20</LanguageStandard>
    </ClCompile>" @replace_file_str

    -- Remove warnings for functions such as fopen, strcat etc.
    location'\\'project_name_c'.vcxproj' >< \
    '_WINDOWS;_USRDLL' \
    '_WINDOWS;_CRT_SECURE_NO_WARNINGS;_USRDLL' @replace_file_str

    -- Remove pch.h and pch.cpp from the listed files, add the source files
    location'\\'project_name_c'.vcxproj' >< \
    '<ClInclude Include="pch.h" />' \
    ("<ClInclude Include=\"nest_source\\error.h\" />
    <ClInclude Include=\"nest_source\\error_internal.h\" />
    <ClInclude Include=\"nest_source\\function.h\" />
    <ClInclude Include=\"nest_source\\hash.h\" />
    <ClInclude Include=\"nest_source\\interpreter.h\" />
    <ClInclude Include=\"nest_source\\iter.h\" />
    <ClInclude Include=\"nest_source\\lexer.h\" />
    <ClInclude Include=\"nest_source\\lib_import.h\" />
    <ClInclude Include=\"nest_source\\llist.h\" />
    <ClInclude Include=\"nest_source\\map.h\" />
    <ClInclude Include=\"nest_source\\nest_include.h\" />
    <ClInclude Include=\"nest_source\\nodes.h\" />
    <ClInclude Include=\"nest_source\\nst_types.h\" />
    <ClInclude Include=\"nest_source\\obj.h\" />
    <ClInclude Include=\"nest_source\\obj_ops.h\" />
    <ClInclude Include=\"nest_source\\parser.h\" />
    <ClInclude Include=\"nest_source\\sequence.h\" />
    <ClInclude Include=\"nest_source\\simple_types.h\" />
    <ClInclude Include=\"nest_source\\str.h\" />
    <ClInclude Include=\"nest_source\\tokens.h\" />
    <ClInclude Include=\"nest_source\\var_table.h\" />
    <ClInclude Include=\""project_name".h\" />" ><) @replace_file_str

    location'\\'project_name_c'.vcxproj' >< \
    "<ClCompile Include=\"pch.cpp\">
      <PrecompiledHeader Condition=\"'$(Configuration)|$(Platform)'=='Debug|x64'\">Create</PrecompiledHeader>
      <PrecompiledHeader Condition=\"'$(Configuration)|$(Platform)'=='Debug|Win32'\">Create</PrecompiledHeader>
      <PrecompiledHeader Condition=\"'$(Configuration)|$(Platform)'=='Release|Win32'\">Create</PrecompiledHeader>
      <PrecompiledHeader Condition=\"'$(Configuration)|$(Platform)'=='Release|x64'\">Create</PrecompiledHeader>
    </ClCompile>" \
    ("<ClCompile Include=\"nest_source\\error.c\" />
    <ClCompile Include=\"nest_source\\function.c\" />
    <ClCompile Include=\"nest_source\\hash.c\" />
    <ClCompile Include=\"nest_source\\interpreter.c\" />
    <ClCompile Include=\"nest_source\\iter.c\" />
    <ClCompile Include=\"nest_source\\lexer.c\" />
    <ClCompile Include=\"nest_source\\lib_import.c\" />
    <ClCompile Include=\"nest_source\\llist.c\" />
    <ClCompile Include=\"nest_source\\map.c\" />
    <ClCompile Include=\"nest_source\\nodes.c\" />
    <ClCompile Include=\"nest_source\\obj.c\" />
    <ClCompile Include=\"nest_source\\obj_ops.c\" />
    <ClCompile Include=\"nest_source\\parser.c\" />
    <ClCompile Include=\"nest_source\\sequence.c\" />
    <ClCompile Include=\"nest_source\\simple_types.c\" />
    <ClCompile Include=\"nest_source\\str.c\" />
    <ClCompile Include=\"nest_source\\tokens.c\" />
    <ClCompile Include=\"nest_source\\var_table.c\" />
    <ClInclude Include=\""project_name".cpp\" />" ><) @replace_file_str
    => 0
]

@main